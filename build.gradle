// Plugins
plugins {
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id 'com.jfrog.artifactory' version '4.13.0' apply false
}

// Artifact Info
project.ext {
	groupId = 'com.github.twitch4j'
	artifactNamespace = 'twitch4j'
	artifactName = 'Twitch4J'
	artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")
	artifactDescription = 'Twitch4J'
	websiteUrl = 'https://github.com/twitch4j/twitch4j'
	issueTrackerUrl = 'https://github.com/twitch4j/twitch4j/issues'
	vcsUrl = 'https://github.com/twitch4j/twitch4j.git'
}

// All-Projects
allprojects {
	// Repositories
	repositories {
		jcenter()
		maven { url "https://maven.google.com" }
		mavenLocal()
	}
}

// Subprojects
subprojects {
    apply plugin: 'java'
	apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'com.jfrog.artifactory'

    // Source Compatiblity
    sourceCompatibility = 1.8

    // Dependency Management for Subprojects
	dependencies {
		constraints {
			// Logging
			api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
			api group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

			// Apache Commons
			api group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
			api group: 'commons-io', name: 'commons-io', version: '2.6'
			api group: 'commons-configuration', name: 'commons-configuration', version: '1.10'

			// Event Dispatcher
			api group: 'com.github.philippheuer.events4j', name: 'events4j-core', version: '0.9.1'
			api group: 'com.github.philippheuer.events4j', name: 'events4j-handler-simple', version: '0.9.1'

			// Credential Manager
			api group: 'com.github.philippheuer.credentialmanager', name: 'credentialmanager', version: '0.1.1'

			// HTTP Client
			api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.6.0'

			// Rate Limiting
			api group: 'com.github.vladimir-bukhtoyarov', name: 'bucket4j-core', version: '4.7.0'

			// Caching
			api group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.2'

			// Http Client
			api group: 'io.github.openfeign', name: 'feign-okhttp', version: '11.0'
			api group: 'io.github.openfeign', name: 'feign-jackson', version: '11.0'
			api group: 'io.github.openfeign', name: 'feign-slf4j', version: '11.0'
			api group: 'io.github.openfeign', name: 'feign-hystrix', version: '11.0'

			// Hystrix
			api group: 'com.netflix.hystrix', name: 'hystrix-core', version: '1.5.18'

			// Jackson (JSON)
			api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.1'
			api group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.11.1'
			api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.1'

			// WebSocket
			api group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.9'

			// Annotations
			api group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
		}
	}

	// Source encoding
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	// Common Dependencies
	plugins.withType(JavaPlugin) {
		dependencies {
			// Annotation processors
			compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
			annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
			testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
			testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

			// Apache Commons
			api group: 'org.apache.commons', name: 'commons-lang3'
			api group: 'commons-io', name: 'commons-io'

			// Logging
			api group: 'org.slf4j', name: 'slf4j-api'
			testImplementation group: 'ch.qos.logback', name: 'logback-classic'

			// Test
			testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.0'
			testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.0'
		}
	}
}
